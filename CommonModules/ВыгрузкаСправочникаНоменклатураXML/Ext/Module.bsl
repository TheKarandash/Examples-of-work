
#Область ПрограммныйИнтерфейс

// Производит выгрузку справочника "Номенклатура" по указанному пути в файл формата XML
//
// Параметры:
//   ПутьКФайлу - Строка - путь к выгружаемому файлу
//
Процедура Выгрузить(Знач ПутьКФайлу) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	
	Попытка
		ЗаписьXML.ОткрытьФайл(ПутьКФайлу);
	Исключение
		ВызватьИсключение НСтр("ru = 'Невозможно получить доступ к файлу. Проверьте правильность имени файла'");
	КонецПопытки;
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	Выборка = Справочники.Номенклатура.ВыборкаНоменклатур();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СписокНоменклатур");
	
	Пока Выборка.Следующий() Цикл
		ЗаписьXML.ЗаписатьНачалоЭлемента("Номенклатура");
		ЗаписьXML.ЗаписатьАтрибут("Код",             Выборка.Код);
		ЗаписьXML.ЗаписатьАтрибут("Наименование",    Выборка.Наименование);
		ЗаписьXML.ЗаписатьАтрибут("Вид",             Строка(Выборка.Вид));
		ЗаписьXML.ЗаписатьАтрибут("ПометкаУдаления", ?(Выборка.ПометкаУдаления, "Да", "Нет"));
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	
	ВыгрузкаСправочникаНоменклатура.СообщениеРезультатыВыгрузки(Выборка.Количество());
	
КонецПроцедуры

#КонецОбласти
