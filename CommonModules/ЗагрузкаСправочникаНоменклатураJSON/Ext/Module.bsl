
#Область ПрограммныйИнтерфейс

// Производит загрузку справочника "Номенклатура" по указанному пути из файла формата JSON
//
// Параметры:
//   ПутьКФайлу - Строка - путь к загружаемому файлу
//
Процедура Загрузить(Знач ПутьКФайлу) Экспорт
	
	ТаблицаНоменклатур = ЗагрузкаСправочникаНоменклатура.НовыйТаблицаНоменклатур();
	ЗаполнитьТаблицуНоменклатур(ПутьКФайлу, ТаблицаНоменклатур);
	
	КоличествоЭлементовУспешно = 0;
	КоличествоЭлементовОшибка  = 0;
	
	ЗагрузкаСправочникаНоменклатура.ОбработатьТаблицуНоменклатур(
		ТаблицаНоменклатур,
		КоличествоЭлементовУспешно,
		КоличествоЭлементовОшибка);
	ЗагрузкаСправочникаНоменклатура.СообщениеРезультатыЗагрузки(
		КоличествоЭлементовУспешно,
		КоличествоЭлементовОшибка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет таблицу номенклатур, используемую для загрузки справочника "Номенклатура"
//
// Параметры:
//   ПутьКФайлу - Строка - путь к выгружаемому файлу
//   ТаблицаНоменклатур - ТаблицаЗначений - таблица, в которую будут загружены номенклатуры из файла
//
Процедура ЗаполнитьТаблицуНоменклатур(Знач ПутьКФайлу, ТаблицаНоменклатур)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	
	Попытка
		ЧтениеJSON.ОткрытьФайл(ПутьКФайлу);
	Исключение
		ВызватьИсключение НСтр("ru = 'Невозможно получить доступ к файлу. Проверьте правильность имени файла'");
	КонецПопытки;
	
	ИмяТекущегоСвойства = "";
	
	Пока ЧтениеJSON.Прочитать() Цикл
		
		ТипТекущегоУзла = ЧтениеJSON.ТипТекущегоЗначения;
		
		Если ТипТекущегоУзла = ТипЗначенияJSON.НачалоОбъекта Тогда
			НоваяСтрока = ТаблицаНоменклатур.Добавить();
		ИначеЕсли ТипТекущегоУзла = ТипЗначенияJSON.ИмяСвойства Тогда
			ИмяТекущегоСвойства = ЧтениеJSON.ТекущееЗначение;
		ИначеЕсли ТипТекущегоУзла = ТипЗначенияJSON.Строка Тогда
			НоваяСтрока[ИмяТекущегоСвойства] = ЧтениеJSON.ТекущееЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеJSON.Закрыть();
	
КонецПроцедуры

#КонецОбласти
