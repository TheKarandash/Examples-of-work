
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаПоДокументу = Товары.Итог("Стоимость");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения()
	
	Движения.Остатки.Записать();
	Движения.Остатки.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА ПоступлениеТоваровТовары.Номенклатура.Вид = &ВидМобильныйТелефон
		|			ТОГДА &Ссылка
		|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
		|	КОНЕЦ КАК Партия,
		|	ПоступлениеТоваровТовары.Количество КАК Количество,
		|	ПоступлениеТоваровТовары.Стоимость КАК Стоимость
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Партия КАК Партия,
		|	СУММА(ВТ_Товары.Количество) КАК Количество,
		|	СУММА(ВТ_Товары.Стоимость) КАК Стоимость
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Товары.Номенклатура,
		|	ВТ_Товары.Партия";
	
	Запрос.УстановитьПараметр("Ссылка",              Ссылка);
	Запрос.УстановитьПараметр("ВидМобильныйТелефон", Перечисления.ВидыНоменклатуры.МобильныйТелефон);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.Остатки.ДобавитьПриход();
		Движение.Период       = Дата;
		Движение.Склад        = Склад;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Партия       = ВыборкаДетальныеЗаписи.Партия;
		Движение.Количество   = ВыборкаДетальныеЗаписи.Количество;
		Движение.Стоимость    = ВыборкаДетальныеЗаписи.Стоимость;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
