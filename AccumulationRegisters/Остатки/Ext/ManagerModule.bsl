
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает исключительную блокировку на регистр "Остатки"
//
// Параметры:
//   Склад - СправочникСсылка.Склады - склад, по которому происходит блокировка
//   ТабличнаяЧасть - ТабличнаяЧасть - источник номенклатур, по которым происходит блокировка
//
Процедура ЗаблокироватьИсключительноПоСкладуИТабличнойЧасти(Склад, ТабличнаяЧасть) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Остатки");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТабличнаяЧасть;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
КонецПроцедуры

// Создает временную таблицу на основании виртуальной таблицы с отбором по складу и номенклатуре
//
// Параметры:
//   МенеджерВТ - МенеджерВременныхТаблиц - менеджер таблиц, в котором хранится таблица товаров
//     и в который будет помещена результирующая таблица
//   Период - МоментВремени - момент времени документа, по которому происходит отбор
//   Склад - СправочникСсылка.Склады - склад, по которому происходит отбор
//
Процедура СоздатьВТОстаткиПоТаблицеТоваров(МенеджерВТ, Период, Склад) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОстаткиОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиОстатки.Партия КАК Партия,
		|	ОстаткиОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.Остатки.Остатки(
		|			&Период,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура
		|					ИЗ
		|						ВТ_Товары)) КАК ОстаткиОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Склад",  Склад);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
