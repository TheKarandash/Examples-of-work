
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает метод списания себестоимости актуальный на дату
// Если метод списания не задан, вызывается исключение
//
// Параметры:
//   Дата - Дата - дата, на которую получается значение
//
// Возвращаемое значение:
//   ПеречислениеСсылка.МетодыСписанияСебестоимости
//
Функция МетодСписанияСебестоимости(Дата) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаСрезПоследних.МетодСписанияСебестоимости КАК МетодСписанияСебестоимости
		|ИЗ
		|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Дата, ) КАК УчетнаяПолитикаСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение НСтр("ru = 'Не указан метод списания себестоимости'");
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.МетодСписанияСебестоимости;
	
КонецФункции

#КонецОбласти

#КонецЕсли
