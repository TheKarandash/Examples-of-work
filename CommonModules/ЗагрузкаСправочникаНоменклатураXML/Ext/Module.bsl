
#Область ПрограммныйИнтерфейс

// Производит загрузку справочника "Номенклатура" по указанному пути из файла формата XML
//
// Параметры:
//   ПутьКФайлу - Строка - путь к загружаемому файлу
//
Процедура Загрузить(Знач ПутьКФайлу) Экспорт
	
	ТаблицаНоменклатур = ЗагрузкаСправочникаНоменклатура.НовыйТаблицаНоменклатур();
	ЗаполнитьТаблицуНоменклатур(ПутьКФайлу, ТаблицаНоменклатур);
	
	КоличествоЭлементовУспешно = 0;
	КоличествоЭлементовОшибка  = 0;
	
	ЗагрузкаСправочникаНоменклатура.ОбработатьТаблицуНоменклатур(
		ТаблицаНоменклатур,
		КоличествоЭлементовУспешно,
		КоличествоЭлементовОшибка);
	ЗагрузкаСправочникаНоменклатура.СообщениеРезультатыЗагрузки(
		КоличествоЭлементовУспешно,
		КоличествоЭлементовОшибка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет таблицу номенклатур, используемую для загрузки справочника "Номенклатура"
//
// Параметры:
//   ПутьКФайлу - Строка - путь к выгружаемому файлу
//   ТаблицаНоменклатур - ТаблицаЗначений - таблица, в которую будут загружены номенклатуры из файла
//
Процедура ЗаполнитьТаблицуНоменклатур(Знач ПутьКФайлу, ТаблицаНоменклатур)
	
	ЧтениеXML = Новый ЧтениеXML;
	
	Попытка
		ЧтениеXML.ОткрытьФайл(ПутьКФайлу);
	Исключение
		ВызватьИсключение НСтр("ru = 'Невозможно получить доступ к файлу. Проверьте правильность имени файла'");
	КонецПопытки;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.Имя = "Номенклатура" 
			И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			НоваяСтрока = ТаблицаНоменклатур.Добавить();
			НоваяСтрока.Код             = ЧтениеXML.ЗначениеАтрибута("Код");
			НоваяСтрока.Наименование    = ЧтениеXML.ЗначениеАтрибута("Наименование");
			НоваяСтрока.Вид             = ЧтениеXML.ЗначениеАтрибута("Вид");
			НоваяСтрока.ПометкаУдаления = ЧтениеXML.ЗначениеАтрибута("ПометкаУдаления");
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
КонецПроцедуры

#КонецОбласти
