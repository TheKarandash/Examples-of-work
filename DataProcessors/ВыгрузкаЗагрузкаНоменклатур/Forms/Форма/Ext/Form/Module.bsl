
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.ПолноеИмяФайла     = ПутьКФайлу;
	ДиалогВыбораФайла.Фильтр             = ОписаниеФорматаФайла().Фильтр;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ПутьКФайлуПриВыборе", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматФайлаПриИзменении(Элемент)
	
	ПутьКФайлу = "";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Отказ = Ложь;
	
	ПроверитьПутьКФайлу(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьНаСервере(ПутьКФайлу, ОписаниеФорматаФайла().Формат);
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	Отказ = Ложь;
	
	ПроверитьПутьКФайлу(Отказ, Ложь);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНаСервере(ПутьКФайлу, ОписаниеФорматаФайла().Формат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ОписаниеФорматаФайла()
	
	Если ФорматФайла = 0 Тогда
		Возврат Новый Структура("Формат, Фильтр", "JSON", "Файлы JSON(*.json)|*.json");
	ИначеЕсли ФорматФайла = 1 Тогда
		Возврат Новый Структура("Формат, Фильтр", "XML", "Файлы XML(*.xml)|*.xml");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПутьКФайлуПриВыборе(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПутьКФайлу(Отказ, Выгрузка = Истина)
	
	Если Не ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(
			НСтр("ru = 'Невозможно выполнить %1. Необходимо указать путь к файлу'"),
			?(Выгрузка, "выгрузку", "загрузку"));
		Сообщение.Поле  = "ПутьКФайлу";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыгрузитьНаСервере(Знач ПутьКФайлу, Знач ФорматФайла)
	
	ПроцессорВыгрузки = ВыгрузкаСправочникаНоменклатура.СоздатьПроцессорВыгрузки(ФорматФайла);
	ПроцессорВыгрузки.Выгрузить(ПутьКФайлу);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(Знач ПутьКФайлу, Знач ФорматФайла)
	
	ПроцессорЗагрузки = ЗагрузкаСправочникаНоменклатура.СоздатьПроцессорЗагрузки(ФорматФайла);
	ПроцессорЗагрузки.Загрузить(ПутьКФайлу);
	
КонецПроцедуры

#КонецОбласти
