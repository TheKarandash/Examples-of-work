
#Область ПрограммныйИнтерфейс

// Производит выгрузку справочника "Номенклатура" по указанному пути в файл формата JSON
//
// Параметры:
//   ПутьКФайлу - Строка - путь к выгружаемому файлу
//
Процедура Выгрузить(Знач ПутьКФайлу) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб, Истина);
	
	Попытка
		ЗаписьJSON.ОткрытьФайл(ПутьКФайлу,,, ПараметрыЗаписи);
	Исключение
		ВызватьИсключение НСтр("ru = 'Невозможно получить доступ к файлу. Проверьте правильность имени файла'");
	КонецПопытки;
	
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Выборка = Справочники.Номенклатура.ВыборкаНоменклатур();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Код");
		ЗаписьJSON.ЗаписатьЗначение(Выборка.Код);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
		ЗаписьJSON.ЗаписатьЗначение(Выборка.Наименование);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Вид");
		ЗаписьJSON.ЗаписатьЗначение(Строка(Выборка.Вид));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("ПометкаУдаления");
		ЗаписьJSON.ЗаписатьЗначение(?(Выборка.ПометкаУдаления, "Да", "Нет"));
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	ЗаписьJSON.Закрыть();
	
	ВыгрузкаСправочникаНоменклатура.СообщениеРезультатыВыгрузки(Выборка.Количество());
	
КонецПроцедуры

#КонецОбласти
