
#Область ПрограммныйИнтерфейс

// Возвращает общий модуль, используемый для выгрузки справочника "Номенклатура"
// Если определить общий модуль не удалось, вызывается исключение
//
// Параметры:
//   ФорматФайла - Строка - формат файла, в который будет производиться выгрузка
//
// Возвращаемое значение:
//   ОбщийМодуль
//
Функция СоздатьПроцессорВыгрузки(ФорматФайла) Экспорт
	
	Если ФорматФайла = "JSON" Тогда
		Возврат ВыгрузкаСправочникаНоменклатураJSON;
	ИначеЕсли ФорматФайла = "XML" Тогда
		Возврат ВыгрузкаСправочникаНоменклатураXML;
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не предусмотрена выгрузка в файл формата %1'"),
			ФорматФайла);
	КонецЕсли;
	
КонецФункции

// Сообщает результаты выгрузки справочника "Номенклатура"
//
// Параметры:
//   КоличествоЭлементов - Число - количество выгруженных элементов
//
Процедура СообщениеРезультатыВыгрузки(КоличествоЭлементов) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон(
		НСтр("ru = 'Выгрузка завершена. Выгружено %1 элементов'"),
		КоличествоЭлементов);
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти
